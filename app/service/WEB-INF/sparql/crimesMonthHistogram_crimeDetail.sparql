select ?datumSpachaniYear ?datumSpachaniMonth  (count(?trestnyCin) as ?crimeNumber)
{
  {
    {
      {
        ?trestnyCin a crime:TrestnyCin .
        ?trestnyCin dct:isVersionOf ?trestnyCin_master .
        ?trestnyCin crime:validFrom ?validFrom .
        ?trestnyCin crime:validTo ?validTo
        
        FILTER ( ( ?validFrom <= ?parHistoryDate_ValuePlaceholder ) && ( ?parHistoryDate_ValuePlaceholder <= ?validTo ) )
      }
        
      OPTIONAL
        { ?trestnyCin crime:datumACasSpachani ?datumSpachani}
      OPTIONAL
        { ?trestnyCin crime:datumACasSpachani_Od ?datumSpachani_Od}
      BIND(coalesce(?datumSpachani, ?datumSpachani_Od) AS ?datumSpachani_Point)
      FILTER ( ( ?parStartDate_ValuePlaceholder <= ?datumSpachani_Point ) && ( ?datumSpachani_Point <= ?parEndDate_ValuePlaceholder ) )
    }
    
    ?utvar a crime:Utvar .
    ?utvar crime:id ?utvarId
    FILTER ( ?utvarId = ?parIdUtvaru_ValuePlaceholder )
    ?trestnyCin crime:utvarSpachaniNeboZjisteni ?utvar .
    
    ?tsk a crime:Tsk .
    ?tsk crime:id ?tskId .
    FILTER ( ?tskId = ?parTskId_ValuePlaceholder ) .
    ?trestnyCin crime:tsk ?tsk .
  }
  
  BIND(YEAR(?datumSpachani_Point) as ?datumSpachaniYear)
  BIND(MONTH(?datumSpachani_Point) as ?datumSpachaniMonth)
}
group by ?datumSpachaniYear ?datumSpachaniMonth 
order by ?datumSpachaniYear ?datumSpachaniMonth